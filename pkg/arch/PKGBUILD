# Maintainer: Otreblan <otreblain@gmail.com>

pkgname=ray-tracing-git
pkgver=0.0.0.r52.ge146dcf
pkgrel=1
epoch=
pkgdesc="Ray tracing"
arch=('x86_64')
url="https://github.com/otreblan/ray-tracing"
license=('GPL3')
groups=()
depends=('fmt' 'embree' 'assimp')
makedepends=('git' 'cmake' 'glm')
checkdepends=()
optdepends=()
provides=(${pkgname%-git})
conflicts=(${pkgname%-git})
_gitrepo=$(git rev-parse --show-toplevel)
_headcommit=$(git rev-parse HEAD)
source=("$pkgname::git+file://$_gitrepo#commit=$_headcommit")
sha256sums=('SKIP')

pkgver() {
	cd "$srcdir/$pkgname"
	( set -o pipefail
	git describe --long 2>/dev/null | sed 's/^v-//;s/\([^-]*-g\)/r\1/;s/-/./g' ||
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
	)
}

build() {
	cmake -B build -S "$pkgname" \
		-DCMAKE_BUILD_TYPE='None' \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON \
		-DCMAKE_EXPORT_COMPILE_COMMANDS=ON

	cmake --build build
}

package() {
	DESTDIR="$pkgdir" cmake --install build
}
